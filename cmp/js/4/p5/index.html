<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8">
    <title>p5</title>

    <link href="../style.css" rel="stylesheet" type="text/css">
    <link href="../../libs/jquery-ui.min.css" rel="stylesheet" type="text/css">
    
    <script type="text/javascript" src="../../libs/jquery-2.1.1.min.js"></script>
    <script type="text/javascript" src="../../libs/jquery-ui.min.js"></script>
    <script type="text/javascript" src="../../libs/processing.min.js"></script>
    <script type="text/javascript" src="script.js"></script>
    <script type="text/processing" data-processing-target="processing-canvas-bezierCurve">
      PVector[] points = new PVector[4];
      int glabedPoint = -1;
      void setup() {
        size(800, 400);
      
        points[0] = new PVector(75, 260);
        points[1] = new PVector(170, 120);
        points[2] = new PVector(350, 90);
        points[3] = new PVector(500, 200);
      }
      
      void draw() {
        background(242);
        for (float t=0; t<1.0; t+=0.001) {
          float[] b = new float[4];
          b[0] = pow(1-t, 3);
          b[1] = 3*t*pow(1-t, 2);
          b[2] = 3*t*t*(1-t);
          b[3] = pow(t, 3);
      
          float x = 0;
          float y = 0;
          for (int i=0; i<4; i++) {
            x += points[i].x*b[i];
            y += points[i].y*b[i];
          }
          point(x, y);
        }
      
        for (int i=0; i<4; i++) {
          fill(0);
          ellipse(points[i].x, points[i].y, 10, 10);
        }
      
        if (mousePressed) {
          for (int i=0; i<4; i++) {
            PVector p = points[i];
            if (glabedPoint == i || dist(p.x, p.y, mouseX, mouseY) < 15) {
              glabedPoint = i;
              points[glabedPoint].x = mouseX;
              points[glabedPoint].y = mouseY;
            }
          }
        } else {
          glabedPoint = -1;
        }
      }
    </script>
    
    <script type="text/processing" data-processing-target="processing-canvas-octahedron">
    float t = 0;
    int[][] r = new int[2][4];
    
    void setup() {
      size(600, 600, P3D);
      frameRate(60);
    }
    
    void draw() {
      background(255);
    
      camera(mouseX*2.3, mouseY*1.8, 200,
             width/2, height/2, 0,
             0, 1, 0);
    
      fill(255, 180, 0);
      translate(width/2, height/2);
    
      for(int y = 0; y < 2; y++) {
        for(int x = 0; x < 4; x++) {
          if(frameCount%3 == 0)
            r[y][x] = floor(random(4));
          int i = r[y][x];
          beginShape();
          vertex(-100, 0, -100, 0, 480/4*i);
          vertex(0, -141, 0, 320/4*i, 0);
          vertex(100, 0, -100, 640/4*i, 480/4*i);
          rotateY(radians(90));
          endShape(CLOSE);
        }
        rotateZ(radians(180));
      }
    }

    </script>
    <script type="text/processing" data-processing-target="processing-canvas-coffeecup">
    int t = 0;
    boolean DEBUG=false;
    
    void setup() {
      size(600, 600);
    }
    
    void draw() {
      background(242);
      translate(width/2, height/2);
    
      // base
      rotate(t/300.*PI);
      fill(169, 215, 225);
      ellipse(0, 0, 400, 400);
    
      if(DEBUG) {
        pushStyle();
          noFill();
          strokeWeight(2);
          line(0, 0, 200, 0);
          stroke(255, 0, 0);
          ellipse(0, 0, 250, 250);
        popStyle();
      }
    
      // cup1
      pushMatrix();
        translate(125, 0);
        rotate(t/100.*PI);
        if(DEBUG)
          fill(211, 235, 241, 200);
        else
          fill(211, 235, 241);
        ellipse(0, 0, 110, 110);
    
        if(DEBUG) {
          pushStyle();
            fill(0);
            ellipse(0, 0, 10, 10);
          popStyle();
        }
    
        // user1
        pushMatrix();
          translate(30, 0);
          fill(255, 0, 0);
          ellipse(0, 0, 30, 30);    
        popMatrix();
    
        // user2
        pushMatrix();
          translate(-30, 0);
          fill(0, 0, 255);
          ellipse(0, 0, 30, 30);    
        popMatrix();
      popMatrix();
    
      //*************************************
      // cup2
      pushMatrix();
        translate(-125, 0);
        rotate(t/100.*PI);
        if(DEBUG)
          fill(211, 235, 241, 200);
        else
          fill(211, 235, 241);
        ellipse(0, 0, 110, 110);
    
        if(DEBUG) {
          pushStyle();
            fill(0);
            ellipse(0, 0, 10, 10);
          popStyle();
        }
    
        // user1
        pushMatrix();
          translate(30, 0);
          fill(255, 0, 0);
          ellipse(0, 0, 30, 30);    
        popMatrix();
    
        // user2
        pushMatrix();
          translate(-30, 0);
          fill(0, 0, 255);
          ellipse(0, 0, 30, 30);    
        popMatrix();
      popMatrix();
    
      if(DEBUG) {
        t = int(1.*mouseX/height*600);
      } else {
        t++;
      }
    }
    
    void keyPressed() {
      if(key == ' ') {
        DEBUG = !DEBUG;
      }
    }
    </script>
</head>
<body>
  <h1>Processing</h1>
  <div id="tabs">
    <ul>
      <li><a href="#tabs-coffee">coffeecup</a></li>
      <li><a href="#tabs-octahedron">octahedron</a></li>
      <li><a href="#tabs-bezierCurve">bezierCurve</a></li>
    </ul>
    <div id="tabs-coffee">
      <canvas id="processing-canvas-coffeecup"></canvas>
    </div>
    <div id="tabs-octahedron">
      <canvas id="processing-canvas-octahedron"></canvas>
    </div>
    <div id="tabs-bezierCurve">
      <canvas id="processing-canvas-bezierCurve"></canvas>
    </div>
  </div>
</body>
</html>
